prototype(Futape.Bootstrap:TableCell) < prototype(Neos.Fusion:Value) {
	# Valid values: normal, active, primary, secondary, success,
	# danger, warning, info, light, dark
	color = 'normal'
	@context.color = ${this.color}

	tagName = 'td'
	@context.tagName = ${this.tagName}

	content = ${null}
	@context.content = ${this.content}

	value = Neos.Fusion:Template {
		templatePath = 'resource://Futape.Bootstrap/Private/Fusion/Table/TableCell.html'

		attributes = Neos.Fusion:Attributes {
			class.@process.addTableClasses = Neos.Fusion:Value {
				classes = Neos.Fusion:RawArray {
					0 = ${'table-' + color}
				}

				value = ${Array.concat(Type.isArray(value) ? value : [value], this.classes)}

				@if.notNormal = ${color != 'normal'}
			}
		}

		tagName = ${tagName}
		content = ${content}
	}
}
